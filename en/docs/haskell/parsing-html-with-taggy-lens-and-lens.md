---
title: "Parsing HTML with taggy-lens and lens"
slug: "parsing-html-with-taggy-lens-and-lens"
draft: false
images: []
weight: 9983
type: docs
toc: true
---

## Extract the text contents from a div with a particular id
Taggy-lens allows us to use lenses to parse and inspect HTML documents.

    #!/usr/bin/env stack
    -- stack --resolver lts-7.0 --install-ghc runghc --package text --package lens --package taggy-lens
    
    {-# LANGUAGE OverloadedStrings #-}
    
    import qualified Data.Text.Lazy as TL
    import qualified Data.Text.IO as T
    import Text.Taggy.Lens
    import Control.Lens
    
    someHtml :: TL.Text
    someHtml =
        "\
        \<!doctype html><html><body>\
        \<div>first div</div>\
        \<div id=\"thediv\">second div</div>\
        \<div id=\"not-thediv\">third div</div>"
    
    main :: IO ()
    main = do
        T.putStrLn
            (someHtml ^. html . allAttributed (ix "id" . only "thediv") . contents)



## Filtering elements from the tree
Find div with `id="article"` and strip out all the inner script tags.

    #!/usr/bin/env stack
    -- stack --resolver lts-7.1 --install-ghc runghc --package text --package lens --package taggy-lens --package string-class --package classy-prelude
    {-# LANGUAGE NoImplicitPrelude #-}
    {-# LANGUAGE OverloadedStrings #-}
    
    import ClassyPrelude
    import Control.Lens hiding (children, element)
    import Data.String.Class (toText, fromText, toString)
    import Data.Text (Text)
    import Text.Taggy.Lens
    import qualified Text.Taggy.Lens as Taggy
    import qualified Text.Taggy.Renderer as Renderer
    
    somehtmlSmall :: Text
    somehtmlSmall =
        "<!doctype html><html><body>\
        \<div id=\"article\"><div>first</div><div>second</div><script>this should be removed</script><div>third</div></div>\
        \</body></html>"
    
    renderWithoutScriptTag :: Text
    renderWithoutScriptTag =
        let mArticle :: Maybe Taggy.Element
            mArticle =
                (fromText somehtmlSmall) ^? html .
                allAttributed (ix "id" . only "article")
            mArticleFiltered =
                fmap
                    (transform
                         (children %~
                          filter (\n -> n ^? element . name /= Just "script")))
                    mArticle
        in maybe "" (toText . Renderer.render) mArticleFiltered
    
    main :: IO ()
    main = print renderWithoutScriptTag
    -- outputs:
    -- "<div id=\"article\"><div>first</div><div>second</div><div>third</div></div>"

Contribution based upon @duplode's [SO answer](http://stackoverflow.com/a/39865972/540810)

